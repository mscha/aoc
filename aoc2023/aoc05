#!/usr/bin/env raku
use v6.d;
$*OUT.out-buffer = False;   # Autoflush

# Advent of Code 2023 day 5 -- https://adventofcode.com/2023/day/5

# Return the parts of a range r1 that are left of,
# overlapping with, and right of a range r2
sub overlay(Range $r1, Range $r2)
{
    my $left = $r1.min < $r2.min
            ?? $r1.min .. min($r1.max, $r2.min - 1)
            !! Nil;
    my $overlap = $r1.min ≤ $r2.max && $r2.min ≤ $r1.max
            ?? max($r1.min, $r2.min) .. min($r1.max, $r2.max)
            !! Nil;
    my $right = $r1.max > $r2.max
            ?? max($r1.min, $r2.max + 1) .. $r1.max
            !! Nil;

    return $left, $overlap, $right;
}

# An entry in a mapping table with a source, destination and length.
# Allows mapping of a single integer or a range of integers.
# In the latter case it returns (up to) three ranges: an unmapped range left
# of this entry's domain, a mapped range within the domain, and an unmapped
# range right of the domain.
class MappingEntry
{
    has Int $.src;
    has Int $.dst;
    has Int $.len;

    has Int $.src-max = $!src + $!len - 1;
    has Range $!src-range = $!src .. $!src-max;

    multi method in-range(Int $id) { $!src ≤ $id ≤ $!src-max }
    multi method in-range(Range $r) { $r.min ≤ $!src-max && $r.max ≥ $!src }

    multi method map(Int $id) { $id - $!src + $!dst }
    multi method map(Range $r)
    {
        my ($left, $overlap, $right) = overlay($r, $!src-range);
        return $left, self.map($overlap.min) .. self.map($overlap.max), $right;
    }
}

# A mapping table with multiple entries.
# Allows mapping of a single integer or a range of integers.
# In the latter case it may return multiple ranges, ones that have been mapped
# by an entry, and unmapped ones that didn't match any entry.
class Mapping
{
    has Str $.from;
    has Str $.to;

    has MappingEntry @.entries;

    method add-entry(Int() $src, Int() $dst, Int() $len)
    {
        @!entries.push: MappingEntry.new(:$src, :$dst, :$len);
        @!entries .= sort(*.src);
    }

    multi method map(Int $id)
    {
        for @!entries -> $e {
            return $e.map($id) if $e.in-range($id);
        }

        # No mapping, return unchanged
        return $id;
    }

    multi method map(Range $r)
    {
        my $remaining = $r;
        gather {
            for @!entries -> $e {
                next unless $e.in-range($remaining);

                my ($left, $middle, $right) = $e.map($remaining);
                take $left if $left;
                take $middle if $middle;
                $remaining = $right;
                last unless $remaining;
            }
            take $remaining if $remaining;
        }
    }
}

# Grammar to parse the almanac
grammar AlmanacSpec
{
    rule TOP { <seed-list> <mapping>+ }

    rule seed-list { 'seeds:' <seed>+ % \s+ }
    rule mapping { <map-from>'-to-'<map-to> 'map:' <map-entry>+ }
    rule map-entry { <dst> <src> <len> }

    token seed { \d+ }
    token map-from { \w+ }
    token map-to { \w+ }
    token dst { \d+ }
    token src { \d+ }
    token len { \d+ }
}

# Almanac class.
# Allows mapping between various types of resources.
class Almanac
{
    has Str $.spec;
    has Bool $.use-ranges = False;
    has Bool $.verbose = False;

    has Int @.seeds;
    has Range @.seed-ranges;
    has %.map;

    submethod TWEAK
    {
        AlmanacSpec.parse($!spec, :actions(self));
        if $!use-ranges {
            # The provided seed IDs are actually ranges with start and length
            for @!seeds -> $s, $len {
                @!seed-ranges.push($s .. $s+$len-1);
            }
        }
        else {
            # Single seed IDs are provided, convert them to (trivial) ranges
            for @!seeds -> $s {
                @!seed-ranges.push($s .. $s);
            }
        }
    }

    # Grammar action methods
    method seed($/) { @!seeds.push(+$/) }
    method mapping($/)
    {
        my $from = ~$<map-from>;
        my $to = ~$<map-to>;
        my $m = Mapping.new(:$from, :$to);
        for $<map-entry> -> $e {
            $m.add-entry($e<src>, $e<dst>, $e<len>);
        }
        %!map{$from} = $m;
    }

    # Convert a range of IDs from one type of resource to another,
    # using one or more mapping tables
    method convert(Str $from, Str $to, *@ranges)
    {
        my $curr = $from;
        my @currRanges = @ranges;
        say "[$curr: ", @currRanges, ']' if $!verbose;
        while $curr ne $to {
            my $m = %!map{$curr};
            @currRanges .= map(-> $r { slip $m.map($r) });
            $curr = $m.to;
            say "[$curr: ", @currRanges, ']' if $!verbose;
        }

        return @currRanges;
    }

    method location-ranges { self.convert('seed', 'location', @!seed-ranges) }
    method min-location { self.location-ranges».min.min };
}

sub MAIN(IO() $inputfile where *.f = 'aoc05.input', Bool :v(:$verbose) = False)
{
    my $alm = Almanac.new(:spec($inputfile.slurp), :$verbose);
    say 'Part 1: ', $alm.min-location;

    my $alm2 = Almanac.new(:spec($inputfile.slurp), :use-ranges, :$verbose);
    say 'Part 2: ', $alm2.min-location;
}
