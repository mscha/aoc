#!/usr/bin/env raku
use v6.d;
$*OUT.out-buffer = False;   # Autoflush

# Advent of Code 2023 day 9 -- https://adventofcode.com/2023/day/9

class Series
{
    has Int @.values;

    sub differences(@val) { @val[1..*] Z- @val }

    sub next-value(*@val)
    {
        my @diff = differences(@val);
        if all(@diff) == 0 {
            return @val.tail;
        }
        else {
            return @val.tail + next-value(@diff);
        }
    }

    sub prev-value(*@val)
    {
        my @diff = differences(@val);
        if all(@diff) == 0 {
            return @val.head;
        }
        else {
            return @val.head - prev-value(@diff);
        }
    }

    method next { @!values.push: next-value(@!values) }
    method prev { @!values.unshift: prev-value(@!values) }

    method Str { @!values.join(', ') ~ ' ...' }
    method gist { @!values.join(', ') ~ ' ...' }
}

sub ints($str) { $str.comb(/'-'? \d+/)Â».Int }

sub MAIN(*@values)
{
    my $series = Series.new(:@values);
    $series.next for ^10;
    $series.prev;
    say $series;
}
