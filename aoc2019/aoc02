#!/usr/bin/env perl6
use v6.d;

# Advent of Code 2019 day 2 -- https://adventofcode.com/2019/day/2

use lib $*PROGRAM.parent;
use ShipComputer;

$*OUT.out-buffer = False;   # Autoflush

multi sub MAIN(*@instructions, Int :$target = 19690720, Bool :v(:$verbose) = False)
{
    my $computer = ShipComputer.new(:@instructions, :$verbose);

    # Part 1
    $computer.program[1,2] = 12,2;  # Alarm state
    $computer.run-program;
    say $computer.program[0];

    # Part 2
    for 0..99 X 0..99 -> ($noun, $verb) {
        $computer.reset;
        $computer.program[1,2] = $noun,$verb;
        $computer.run-program;
        if $computer.program[0] == $target {
            say "($noun, $verb) => ", 100Ã—$noun + $verb;
        }
    }
}

multi sub MAIN(Str $inputfile where *.IO.f, Int :$target = 19690720, Bool :v(:$verbose) = False)
{
    MAIN($inputfile.IO.lines, :$target, :$verbose);
}

multi sub MAIN(Int :$target = 19690720, Bool :v(:$verbose) = False)
{
    MAIN(~$*PROGRAM.sibling('aoc02.input'), :$target, :$verbose);
}
