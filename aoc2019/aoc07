#!/usr/bin/env perl6
use v6.d;

# Advent of Code 2019 day 7 -- https://adventofcode.com/2019/day/7

use lib $*PROGRAM.parent;
use ShipComputer;

$*OUT.out-buffer = False;   # Autoflush

# An Amplifier is a ShipComputer that is optionally chained to other
# Amplifiers.  The output of the previous amplifier is the input of this one
class Amplifier is ShipComputer {
    has $.prev-amp is rw;
    has $.output-channel = Channel.new;

    submethod TWEAK
    {
        # Override the input handler
        self.set-input-handler: sub {
            # Only if we don't have any local input, and if we have a
            # chained previous amplifier, ask it for an output value
            if !self.input && $!prev-amp {
                return $!prev-amp.output-channel.receive;
            }
            # Otherwise, just use a local input value
            else {
                return self.input-value;
            }
        };

        # Override the output handler
        self.set-output-handler: sub ($v) {
            # In addition to saving the output as normal, also send it to
            # the output channel
            $!output-channel.send($v);
            self.output.append($v);
        }
    }

    method reset
    {
        # Clear the output channel of any remaining values
        if $!output-channel {
            while $!output-channel.poll { }
        }
        # Perform the usual ShipComputer reset stuff
        nextsame;
    }
}

multi sub MAIN(*@instructions, Bool :v(:$verbose) = False)
{
    # Part 1
    my $amp = Amplifier.new(:@instructions);
    my $best-output = -∞;
    for (^5).permutations -> @phases {
        my $signal = 0;
        for (^5) -> $i {
            $amp.reset;
            $amp.input = @phases[$i], $signal;
            $amp.run-program;
            $signal = $amp.output.tail;
        }
        say @phases, ' => ', $signal if $verbose;
        $best-output max= $signal;
    }
    say "Part 1: ", $best-output;

    # Part 2
    my @amps = Amplifier.new(:@instructions) xx 5;
    for ^5 -> $i {
        @amps[$i].prev-amp = @amps[($i-1) % 5];
    }
    $best-output = -∞;
    for (5..^10).permutations -> @phases {
        my $signal = 0;
        for (^5) -> $i {
            @amps[$i].reset;
            @amps[$i].input = @phases[$i];
        }
        @amps[0].input.append: 0;
        await @amps.map: -> $amp {
            start { $amp.run-program; }
        }
        $signal = @amps.tail.output.tail;
        say @phases, ' => ', $signal if $verbose;
        $best-output max= $signal;
    }
    say "Part 2: ", $best-output;
}

multi sub MAIN(Str $inputfile where *.IO.f, Bool :v(:$verbose) = False)
{
    MAIN($inputfile.IO.lines, :$verbose);
}

multi sub MAIN(Bool :v(:$verbose) = False)
{
    MAIN(~$*PROGRAM.sibling('aoc07.input'), :$verbose);
}
