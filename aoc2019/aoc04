#!/usr/bin/env perl6
use v6.d;

# Advent of Code 2019 day 3 -- https://adventofcode.com/2019/day/3

$*OUT.out-buffer = False;   # Autoflush

sub has-repeated-digit(Int $n)
{
    return !([≠] $n.comb(/\d/));
}

sub is-non-decreasing(Int $n)
{
    return [≤] $n.comb(/\d/);
}

sub has-repeated-digit-group-of-size(Int $n, Int $size)
{
    return ?$n.comb(/ \d+ % <same> /).grep(*.chars == $size);
}

sub MAIN(Int $min = 172851, Int $max = 675869)
{
    # Part 1
    my @candidates = ($min..$max).grep(&is-non-decreasing).grep(&has-repeated-digit);
    say +@candidates;

    # Part 2
    @candidates .= grep(*.&has-repeated-digit-group-of-size(2));
    say +@candidates;
}

