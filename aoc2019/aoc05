#!/usr/bin/env perl6
use v6.d;

# Advent of Code 2019 day 4 -- https://adventofcode.com/2019/day/4

$*OUT.out-buffer = False;   # Autoflush

class ShipComputer
{
    has Int @.program;
    has Int @.input-values;

    has Bool $.verbose = False;
    has Bool $.debug = False;

    has Int $.pos = 0;
    has Int $.input-pos = 0;

    has Int @!initial-program;
    has Int @!initial-input-values;

    enum Opcode (
        ADD => 1,
        MUL => 2,
        INP => 3,
        OUT => 4,
        JIT => 5,
        JIF => 6,
        LTH => 7,
        EQU => 8,
        HLT => 99
    );

    submethod TWEAK
    {
        @!initial-program = @!program;
        @!initial-input-values = @!input-values;
    }

    method reset
    {
        @!program = @!initial-program;
        $!pos = 0;

        @!input-values = @!initial-input-values;
        $!input-pos = 0;
    }

    method opcode
    {
        return @!program[$!pos] % 100;
    }

    method param-mode(Int $n)
    {
        return @!program[$!pos] div 10**($n+1) % 10;
    }

    method param(Int $n, Int $mode = $.param-mode($n)) is rw
    {
        return-rw $mode ?? @!program[$!pos+$n] !! @!program[@!program[$!pos+$n]]
    }

    method input
    {
        die "Invalid input position $!input-pos!" if $!input-pos ≥ @!input-values;
        return @!input-values[$!input-pos++];
    }

    method run-program
    {
        say "< @!program[] >" if $!debug;
        INSTRUCTION:
        loop {
            die "Invalid program position $!pos" unless 0 ≤ $!pos < @!program;
            given $.opcode {
                when ADD {
                    say ">> $!pos: ADD - [$.param(3,1)] = $.param(1) + $.param(2)" if $!verbose;
                    $.param(3) = $.param(1) + $.param(2);
                    $!pos += 4;
                }
                when MUL {
                    say ">> $!pos: MUL - [$.param(3,1)] = $.param(1) × $.param(2)" if $!verbose;
                    $.param(3) = $.param(1) × $.param(2);
                    $!pos += 4;
                }
                when INP {
                    my $val = $.input;
                    say ">> $!pos: INP - [$.param(1,1)] = $val" if $!verbose;
                    $.param(1) = $val;
                    $!pos += 2;
                }
                when OUT {
                    say ">> $!pos: OUT - $.param(1)" if $!verbose;
                    say "Output: $.param(1)";
                    $!pos += 2;
                }
                when JIT {
                    say ">> $!pos: JIT - ? $.param(1) --> $.param(2)" if $!verbose;
                    if $.param(1) {
                        $!pos = $.param(2);
                    }
                    else {
                        $!pos += 3;
                    }
                }
                when JIF {
                    say ">> $!pos: JIF - ! $.param(1) --> $.param(2)" if $!verbose;
                    if !$.param(1) {
                        $!pos = $.param(2);
                    }
                    else {
                        $!pos += 3;
                    }
                }
                when LTH {
                    say ">> $!pos: LTH - [$.param(3,1)] = $.param(1) < $.param(2)" if $!verbose;
                    $.param(3) = +($.param(1) < $.param(2));
                    $!pos += 4;
                }
                when EQU {
                    say ">> $!pos: EQU - [$.param(3,1)] = $.param(1) == $.param(2)" if $!verbose;
                    $.param(3) = +($.param(1) == $.param(2));
                    $!pos += 4;
                }
                when HLT {
                    say ">> $!pos: HLT";
                    last INSTRUCTION;
                }
                default {
                    die "Invalid instruction '@!program[$!pos]' at $!pos!";
                }
            }
            say "< @!program[] >" if $!debug;
        }
    }
}

multi sub MAIN(*@instructions, Int :$target = 19690720, Bool :v(:$verbose) = False)
{
    my @program = @instructions.join(' ').comb(/'-'?\d+/)».Int;
    my $computer = ShipComputer.new(:@program, :input-values(1,), :$verbose);

    # Part 1
    say "PART 1";
    $computer.run-program;

    # Part 2
    say '=' x 60;
    say "PART 2";
    $computer = ShipComputer.new(:@program, :input-values(5,), :$verbose);
    $computer.run-program;
}

multi sub MAIN(Str $inputfile where *.IO.f, Int :$target = 19690720, Bool :v(:$verbose) = False)
{
    MAIN($inputfile.IO.lines, :$target, :$verbose);
}

multi sub MAIN(Int :$target = 19690720, Bool :v(:$verbose) = False)
{
    MAIN(~$*PROGRAM.sibling('aoc05.input'), :$target, :$verbose);
}
