#!/usr/bin/env perl6
use v6.c;

$*OUT.out-buffer = False;   # Autoflush

class Grid
{
    has @.points;
    has @.closest-point;
    has @.total-distance;
    has @.area;
    
    has $!min-x = ∞;
    has $!max-x = -∞;
    has $!min-y = ∞;
    has $!max-y = -∞;

    sub distance (($x1, $y1), ($x2, $y2))
    {
        return abs($x1-$x2) + abs($y1-$y2);
    }

    method add-point($x, $y)
    {
        @!points.push: ($x,$y);
        $!min-x min= $x; $!max-x max= $x;
        $!min-y min= $y; $!max-y max= $y;
    }

    method calc-distances
    {
        for $!min-x .. $!max-x X $!min-y .. $!max-y -> ($x, $y) {
            my @dist = @!points.map(*.&distance(($x, $y)));
            my @closest = @dist.minpairs;
            @!closest-point[$x;$y] = @closest == 1 ?? @closest[0].key !! -1;
            @!total-distance[$x;$y] = @dist.sum;
        }
    }

    method calc-areas
    {
        self.calc-distances unless @!closest-point;

        POINT:
        for @!points.kv -> $i, $p {
            for $!min-x .. $!max-x X $!min-y .. $!max-y -> ($x, $y) {
                if @!closest-point[$x;$y] == $i {
                    if $x == any($!min-x, $!max-x, $!min-y, $!max-y) {
                        # Border point, so area is infinite
                        @!area[$i] = ∞;
                        next POINT;
                    }
                    else {
                        @!area[$i]++;
                    }
                }
            }
        }
    }

    method largest-finite-area
    {
        self.calc-areas unless @!area;
        return @!area.grep(* < ∞).max;
    }

    method area-with-distance-under($limit)
    {
        self.calc-distances unless @!total-distance;
        return +@!total-distance[$!min-x .. $!max-x; $!min-y .. $!max-y].grep(* < $limit);
    }
}

#| Process coordinates
multi sub MAIN(*@coords, Int :$dist-limit=10_000)
{
    my $grid = Grid.new;
    for @coords».Int -> $x, $y {
        $grid.add-point($x, $y);
    }

    say "The size of the largest area is: $grid.largest-finite-area()";
    say "The size of the region with total distance < $dist-limit is: $grid.area-with-distance-under($dist-limit).";
}

#| Process coordinates from a file
multi sub MAIN(Str $inputfile where *.IO.f, Int :$dist-limit=10_000)
{
    MAIN($inputfile.IO.slurp.comb(/\d+/), :$dist-limit);
}

#| Process default coordinate file (aoc6.input)
multi sub MAIN()
{
    MAIN(~$*PROGRAM.sibling('aoc6.input'));
}
