#!/usr/bin/env perl6
use v6.c;

# Advent of Code 2018 day 2 -- https://adventofcode.com/2018/day/2

$*OUT.out-buffer = False;   # Autoflush

sub is-neighbour($a, $b)
{
    ($a.comb Z $b.comb).grep({ $_[0] ne $_[1] }) == 1;
}

sub common-string($a, $b)
{
    ($a.comb Z $b.comb).grep({ $_[0] eq $_[1] })Â»[0].join;
}

multi sub MAIN(*@ids, Bool :v(:$verbose) = False)
{
    my ($a, $b) = @ids.combinations(2).first(-> ($a, $b) { is-neighbour($a, $b) });
    say "$a & $b => { common-string($a, $b) }";
}

multi sub MAIN(Str $inputfile where *.IO.f, Bool :v(:$verbose) = False)
{
    MAIN($inputfile.IO.words, :$verbose);
}

multi sub MAIN(Bool :v(:$verbose) = False)
{
    MAIN(~$*PROGRAM.sibling('aoc02.input'), :$verbose);
}
