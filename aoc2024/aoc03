#!/usr/bin/env raku
use v6.d;
$*OUT.out-buffer = False;   # Autoflush

# Advent of Code 2024 day 3 -- https://adventofcode.com/2024/day/3

sub MAIN(IO() $inputfile where *.f = 'aoc03.input')
{
    my $program = $inputfile.slurp;

    my token num { \d ** 1..3 }

    my token mul { 'mul(' <num> ',' <num> ')' }
    my token do { "do()" }
    my token dont { "don't()" }

    my $part1 = 0;
    my $part2 = 0;
    my $do = True;
    for $program ~~ m:g / <mul> || <do> || <dont> / -> $/ {
        $do = True if $<do>;
        $do = False if $<dont>;

        with $<mul> -> $/ {
            $part1 += [×] $<num>;
            $part2 += [×] $<num> if $do;
        }
    }

    say "Part 1: $part1";
    say "Part 2: $part2";
}
